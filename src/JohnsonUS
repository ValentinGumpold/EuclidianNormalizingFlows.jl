struct JohnsonSU{T1<:Real,T2<:Real,T3<:Real,T4<:Real} <: Distributions.Distribution{Univariate,Continuous}
    gamma::T1 #positive: left tail, negativ: right tail, 0: no tail
    delta::T2 #height and width
    xi::T3 #shift along the x-axis
    lambda::T4 #height and width
end


function JohnsonSU(; gamma::Real = 10, delta::Real = 3.5, xi::Real = 10, lambda::Real = 1)
    gamma, delta, xi, lambda = promote(gamma, delta, xi, lambda)
    JohnsonSU(gamma, delta, xi, lambda)
end


Base.minimum(d::JohnsonSU) = -Inf
Base.maximum(d::JohnsonSU) = +Inf

StatsBase.params(d::JohnsonSU) = (d.gamma, d.delta, d.xi, d.lambda)
@inline Distributions.partype(d::JohnsonSU{T}) where {T<:Real} = T

Distributions.location(d::JohnsonSU) = mean(d)
Distributions.scale(d::JohnsonSU) = var(d)

Statistics.mean(d::JohnsonSU) = d.xi - d.lambda * exp(d.delta^(-2)/2)*sinh(d.gamma/d.delta)
Statistics.median(d::JohnsonSU) = d.xi + d.lambda * sinh(-d.gamma/d.delta)
Statistics.var(d::JohnsonSU) = (d.lambda^2)/2 * (exp(d.delta^(-2)) - 1) * (exp(d.delta^(-2))*cosh(2*d.gamma/d.delta) + 1)


johnsontrafo(d::JohnsonSU, x::Real) = (d.gamma + d.delta*asinh((x - d.xi)/d.lambda))
johnsontrafo_inv(d::JohnsonSU, x::Real) = d.lambda*sinh((x - d.gamma)/d.delta) + d.xi
deriv_johnsontrafo(d::JohnsonSU, x::Real) = (d.delta/d.lambda) * (1/(sqrt(1 + ((x - d.xi)/d.lambda)^2)))
deriv_johnsontrafo_inv(d::JohnsonSU, x::Real) = d.lambda*cosh((x - d.gamma)/d.delta)/d.delta
johnsontrafo_ladj(d::JohnsonSU, x::Real) = log(abs(deriv_johnsontrafo(d::JohnsonSU, x::Real)))
johnsontrafo_inv_ladj(d::JohnsonSU, x::Real) = log(abs(deriv_johnsontrafo_inv(d::JohnsonSU, x::Real)))


Distributions.pdf(d::JohnsonSU, x::Real) = deriv_johnsontrafo(d,x) * pdf(Normal(), johnsontrafo(d,x)^2)
Distributions.cdf(d::JohnsonSU, x::Real) = cdf(Normal(), johnsontrafo(d,x))
Distributions.logpdf(d::JohnsonSU, x::Real) = log(deriv_johnsontrafo(d,x) * pdf(Normal(), johnsontrafo(d,x)^2))
Distributions.logcdf(d::JohnsonSU, x::Real) = logcdf(Normal(), johnsontrafo(d,x))
Distributions.ccdf(d::JohnsonSU, x::Real) = 1 - Distributions.cdf(d, x)
Distributions.logccdf(d::JohnsonSU, x::Real) = log(1 - Distributions.cdf(d, x))


Statistics.quantile(d::JohnsonSU, x::Real) = johnsontrafo_inv(d, quantile(Normal(),x))


